// //// JC version: 3
#include <console.jch>
#include <json.jch>
#include '../ast/node.jch'
#include './cleanup.jch'

namespace ama {
	ama::Node*+! CleanupDummyRaws(ama::Node*+! nd_root) {
		//need cleanup inside nodeofs
		for(ama::Node*+! nd_raw : nd_root.FindAllWithin(0, ama::N_RAW, NULL)) {
			if( (nd_raw.flags & 0xffff) != 0 || !nd_raw.p ) { continue; }
			if( !nd_raw.c && !(nd_raw.p.node_class == ama::N_CALL && nd_raw == nd_raw.p.c) ) {
				nd_raw.Delete();
			} else if( nd_raw.c && !nd_raw.c.s ) {
				nd_raw.ReplaceWith(nd_raw.c);
			}
		}
		while( nd_root.c && !nd_root.c.s && nd_root.c.node_class == ama::N_RAW && !(nd_root.c.flags & 0xffff) ) {
			nd_root = nd_root.c;
		}
		return nd_root;
	}
	private void! dfsPullComment(ama::Node*+! nd) {
		for(ama::Node*+! ndi : nd) {
			dfsPullComment(ndi);
		}
		if( nd.c && 
		(nd.node_class == ama::N_CALL || nd.node_class == ama::N_CALL_TEMPLATE || nd.node_class == ama::N_CALL_CUDA_KERNEL || 
		nd.node_class == ama::N_BINOP || (nd.node_class == ama::N_UNARY && !(nd.flags & ama::UNARY_POSTFIX)) || 
		nd.node_class == ama::N_DOT || nd.node_class == ama::N_ITEM || 
		(nd.node_class == ama::N_RAW && (nd.flags & 0xffff) == 0)) ) {
			if( nd.c.comments_before.length ) {
				nd.comments_before = new char[|]!(nd.comments_before + nd.c.comments_before);
				nd.c.comments_before = '';
			}
		}
	}
	ama::Node*+! SanitizeCommentPlacement(ama::Node*+! nd_root) {
		//COULDDO: make \n a trailing comment for statements: what about the first line in a scope?
		//pull comments_before out from nd.c for "headless" nodes
		dfsPullComment(nd_root);
		return nd_root;
	}
	ama::Node*+! StripBinaryOperatorSpaces(ama::Node*+! nd_root) {
		for(ama::Node*! nd : nd_root.FindAll(ama::N_BINOP, NULL)) {
			ama::Node*+! nd_a = nd.c;
			if( nd_a.comments_after.endsWith(' ') ) {
				nd_a.comments_after = new char[|]!(nd_a.comments_after.subarray(0, nd_a.comments_after.length - 1));
			}
			ama::Node*+! nd_b = nd.c.s;
			if( nd_b.comments_before.startsWith(' ') ) {
				nd_b.comments_before = new char[|]!(nd_b.comments_before.subarray(1));
			}
		}
		return nd_root;
	}
	ama::Node*+! RemoveRedundantSemicolon(ama::Node*+! nd_root) {
		for(ama::Node*+! nd : nd_root.FindAll(ama::N_SYMBOL, ";")) {
			if( nd.p.node_class == ama::N_SCOPE ) {
				nd.Delete();
				nd.FreeASTStorage();
			}
		}
		return nd_root;
	}
};
